# Copyright (c) 2024-present The FlashCoin developers
# Distributed under the MIT software license, see the accompanying
# file COPYING or https://opensource.org/license/mit/.

set(CMAKE_AUTOMOC_MOC_OPTIONS "-p${CMAKE_CURRENT_SOURCE_DIR}")

add_executable(test_FlashCoin-qt
  apptests.cpp
  optiontests.cpp
  rpcnestedtests.cpp
  test_main.cpp
  uritests.cpp
  util.cpp
  ../../init/FlashCoin-qt.cpp
)

target_link_libraries(test_FlashCoin-qt
  core_interface
  FlashCoinqt
  test_util
  FlashCoin_node
  Boost::headers
  Qt6::Test
)

import_plugins(test_FlashCoin-qt)

if(ENABLE_WALLET)
  target_sources(test_FlashCoin-qt
    PRIVATE
      addressbooktests.cpp
      wallettests.cpp
      ../../wallet/test/wallet_test_fixture.cpp
  )
endif()

add_test(NAME test_FlashCoin-qt
  COMMAND test_FlashCoin-qt
)
if(WIN32 AND VCPKG_TARGET_TRIPLET)
  # On Windows, vcpkg configures Qt with `-opengl dynamic`, which makes
  # the "minimal" platform plugin unusable due to internal Qt bugs.
  set_tests_properties(test_FlashCoin-qt PROPERTIES
    ENVIRONMENT "QT_QPA_PLATFORM=windows"
  )
endif()

install_binary_component(test_FlashCoin-qt INTERNAL)
